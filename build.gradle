plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1' apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.mappings}")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = "${rootProject.minecraft_version}-${rootProject.mod_version}" as Object
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    repositories {
        // Parchment
        maven {
            url = "https://maven.parchmentmc.net/"
            content {
                includeGroup "org.parchmentmc.data"
            }
        }

        // ThreeTAG stuff
        maven {
            url 'https://repo.repsy.io/mvn/lucraft/threetag'
        }

        // CurseMaven
        maven {
            url "https://www.cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }

        // Trinkets
        maven {
            url "https://maven.terraformersmc.com/releases/"
        }

        // REI
        maven {
            url "https://maven.shedaniel.me"
        }

        // JEI
        maven {
            url "https://dvs1.progwml6.com/files/maven/"
        }

        // Cardinal Components
        maven {
            name = 'Ladysnake Mods'
            url = 'https://ladysnake.jfrog.io/artifactory/mods'
        }

        // Curios
        maven {
            url = "https://maven.theillusivec4.top/"
        }

        // KubeJS / Rhino
        maven {
            url = 'https://maven.saps.dev/minecraft'
            content {
                includeGroup 'dev.latvian.mods'
            }
        }

        // Forge Config API Port
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }

        // Ingredient Extension API
        maven {
            url = 'https://maven.blamejared.com'
        }

        // Player Animator
        maven {
            name "KosmX's maven"
            url 'https://maven.kosmx.dev/'
        }

        maven { url 'https://jitpack.io' }
    }

    java {
        withSourcesJar()
    }
}